#javaj#

    <frames>
      FrameStdEE, Emergency editor, 600, 500

    <layout of FrameStdEE>
          EVA, 10, 10, 7, 7

          ---,               , X
             , lStdEE.lFile  , eStdEE.eFile, bStdEE.bSearch, bStdEE.bSave
           X , fStdEE.fEditor, -, -, -, -

#data#

    <lStdEE.lFile> File

    <bStdEE.bSearch> Search
    <bStdEE.bSearch DIALOG> FILE

    <bStdEE.bSave> Save

    <bStdEE.bSearch image> javaj/img/linterna.png
    <bStdEE.bSave image>   javaj/img/floppy.png

#listix#

   <-- bStdEE.bSearch>
       -->, eStdEE.eFile data!,, @<bStdEE.bSearch chosen>
       @<StdEE.load file>

   <-- bStdEE.bSave>
       -->, fStdEE.fEditor save, @<eStdEE.eFile>

   <StdEE.show (file)>
      -->, eStdEE.eFile data!,, @<p1>
      @<StdEE.load file>
      MSG, FrameStdEE show


   <StdEE.load file>
       CHECK, VAR  , eStdEE.eFile
       CHECK, RFILE, @<eStdEE.eFile>, BOX, File @<eStdEE.eFile> not found
       -->, fStdEE.fEditor load, @<eStdEE.eFile>

   <run StdEE.edit(file line)>
      -->, eStdEE.eFile data!,, @<p1>
      -->, fStdEE.fEditor load, @<p1>
      -->, fStdEE.fEditor gotoLine, @<p2>
      MSG, FrameStdEE show
      