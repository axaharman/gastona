#javaj#

   <frames> FSample, Gastona sample's browser, 800, 500

   <layout of FSample>
		EVA, 10, 10, 6, 6

		   , X
         , lSamples
       X , sAsisteSamples
       X , xSample
         , bLaunch

   <sysDefaultFonts>  Courier, 13, 0, TextArea

#data#

   <lSamples> Copying documentation db ...

#listix#

   <main0>
      LOAD, data, @<:sys user.home>/.gastonaLocal
      LOAD, data, .WelcomeGastona
      CHECK, VAR, tool java, SET VAR, tool java, java
           , CONTINUE, 1

      SET VAR, TempGast        , @<:listix tmp gast>
      SET VAR, xSample fileName, @<TempGast>
      SET VAR, TempDB          , @<:lsx tmp>

   <main>
      RESUTIL, COPY, META-GASTONA/WelcomeGastona/genDB/gastonaDoc.db, @<TempDB>
      SET VAR, sAsisteSamples dbName, @<TempDB>
      -->, sAsisteSamples, sqlSelect, //SELECT gastSample, sampleBody FROM tGastExamples
      -->, lSamples,, Samples

	<-- sAsisteSamples>
	   GEN, @<TempGast>, sampleAsText
	   MSG, xSample load

	<sampleAsText>
	   CHECK, VAR, sAsisteSamples selected.sampleBody
	   STRCONV, STRE-TEXT, sAsisteSamples selected.sampleBody

   <"> CHECK, LINUX, //"

	<-- bLaunch>
      MSG, xSample save
      LAUNCH, //@<tool java> -cp @<">@<:sys java.class.path>@<"> gastona.gastona @<">@<TempGast>@<">

	   