#javaj#

   <frames>
      Fmain, Gastona Welcome

   <sysDefaultFonts>
      Monospaced, 13, 0, TextArea.font

   <layout of Fmain>
      EVA, 10, 10, 7, 7

        -, 200         ,  X
         , menu        ,  -              , lErrorState
         , eFilterTree , lContext & Name , -
       X , aSrcTree    , layHelpAndSamples, -
         ,   +         , options          , -


   <layout of options>
      PANEL, X

      kDescription, kDetails

   <layout of menu>
      MENU,

      Help   , About Gastona , menuAbout

   <layout of layHelpAndSamples>
      TABBED,

      Help           , xHelp
      Samples        , laySamples
      Output consoles, consoles

   <layout of laySamples>
      EVA, 10, 10, 5, 5

      ----, 60     , , X
          , iSamples, - , lSampleTitle
       X  ,    +    ,   , xSample
          , eSearchSample, bSearchInSamples, bRun Sample

   <layout of consoles>
      EVA, 6, 6, 4, 4

         ,                 , X
         , lStandard Output,   ,bClearConsole1
       X , o1ConsoleOut, -, -
       X , +
         , lStandard Error,   ,bClearConsole2
       X , o2ConsoleErr, -, -

#data#

   <bSearchInSamples image> javaj/img/linterna.png
   <bSearchInSamples> "All"

   <bClearConsole1> Clear
   <bClearConsole2> Clear

   <kDescription enabled> 0
   <kDescription selected> 1
   <kDetails enabled> 0
   <kDetails selected> 0

   <Fmain posX> 200
   <Fmain posY> 100

   <xSample tabulator> 3
   <xHelp tabulator> 3

   <aSrcTree separator> //_
   <aSrcTree rootTitle> Gastona documentation
   <aSrcTree folderIcons> javaj/img/book

   <iSamples visibleColumns> gastSample

   <node field> //structType || SUBSTR('_', 1, MIN(1,MAX(LENGTH(groupInfo),0))) || groupInfo || "_" || name

#listix#


   <main0>
      @<default position frame>
      LOAD, data, @<:sys user.home>/.gastonaLocal
      LOAD, data, .WelcomeGastona

      SETVAR, gastonaDoc.db      , @<:listix tmp>
      SETVAR, gastTemp.gast      , @<:listix tmp gast>
      SETVAR, helpTemp.txt       , @<:listix tmp>

      SETVAR, xSample fileName, @<gastTemp.gast>
      SETVAR, xHelp fileName  , @<helpTemp.txt>

      <! Extract documentation db
      RESUTIL, COPY, META-GASTONA/WelcomeGastona/genDB/gastonaDoc.db, @<gastonaDoc.db>

      SETVAR, iSamples dbName, @<gastonaDoc.db>
      SETVAR, aSrcTree dbName, @<gastonaDoc.db>

      CHECK, VAR, tool java, SET VAR, tool java, java

   <main>
      @<updateTree>
      @<-- bClearConsole1>
      @<-- bClearConsole2>


   <-- eFilterTree>  @<updateTree>

   <updateTree>
      -->, aSrcTree, sqlSelect, //@<sql for tree>


   <sql for tree>
      //SELECT * FROM (SELECT @<node field> AS node FROM tGastItemDoc @<Where&OrderBy from FilterTree>) ORDER BY node;


   <default position frame>
      SETNUM, Fmain posX, 0.1 * @<:lsx screenX>
      SETNUM, Fmain posY, 0.1 * @<:lsx screenY>
      SETNUM, Fmain sizeX, .7 * @<:lsx screenX>
      SETNUM, Fmain sizeY, .6 * @<:lsx screenY>

   <-- bClearConsole1> MSG, o1ConsoleOut clear
   <-- bClearConsole2>
         MSG, o2ConsoleErr clear
         -->, lErrorState,,""

   <-- ERROR_DETECTED>
      -->, lErrorState,, *** Error(s) on the console!

   <-- aSrcTree>      @<show Doc Item>
   <-- kDescription>  @<show Doc Item>
   <-- kDetails>      @<show Doc Item>

   <turn checks(enabled)>
      -->, kDescription, enabled, @<p1>
      -->, kDetails    , enabled, @<p1>

   <show Doc Item>
      <! Intended Loop for 1 record
      LOOP, SQL, @<gastonaDoc.db>, //SELECT * FROM tGastItemDoc WHERE @<node field> = '@<aSrcTree selectedPath>';
      @<update labels>
      @<getItemDoc fields>
      @<build help text>
      @<get samples>

   <update labels>
      -->, lContext & Name, , @<structType> : @<name>

   <getItemDoc fields>
      SET VAR, this structType, @<structType>
      SET VAR, this groupInfo, @<groupInfo>
      SET VAR, this name, @<name>

   <build help text>
      GEN, @<helpTemp.txt>, help format
      MSG, xHelp load
      IF, @<structType>
        , javaj_widget   , LISTIX, turn checks(enabled), 1
        , listix_command , LISTIX, turn checks(enabled), 1
        , ELSE           , LISTIX, turn checks(enabled), 0

   <help format>
      //
      // NAME        : @<name>
      // DOC TYPE    : @<structType>  GROUP: @<groupInfo>  FREQUENCY: @<importance>
      // JAVA CLASS  : @<javaClass>
      // DESCRIPTION : @<desc>
      //
      //@<print help literal>
      //@<attributes and messages>
      //@<syntaxes parameters and options>


   <print help literal>
      CHECK, !=, @<structType>-@<kDescription selected>, javaj_widget-0
      CHECK, !=, @<structType>-@<kDescription selected>, listix_command-0
      STRCONV, STRE-TEXT, helpText
      //
      //

   <! javaj widgets attributes and messages
   <!

   <attributes and messages>
      CHECK, =, @<structType>, javaj_widget
      //   LIST OF ATTRIBUTES:
      //   ----------------------------------------
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT * FROM tZWidgetAttributes WHERE widName = '@<name>';
      //      @<flechita> <NAME@<name join>> @<possibleValues> @<desc detail>
      =,,
      //
      //
      //   LIST OF MESSAGES:
      //   ----------------------------------------
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT * FROM tZWidgetMessages WHERE widName = '@<name>';
      //      @<flechita> "NAME@<msg join>" @<desc detail>
      =,,

   <desc detail>
      CHECK, =, @<kDetails selected>, 1
      //
      //          @<desc>
      //


   <name join> LISTIX, join, @<name>
   <msg join>  LISTIX, join, @<msg>

   <join>
      IN CASE, @<p1>
             , ""  , //
             , ELSE, // @<p1>

   <flechita>
      IN CASE, @<in_out>
             , in   , "--o"
             , out  , "o--"
             , inout, "o-o"
             , ELSE , "o?o"

   <! listix command syntaxes and options
   <!

   <syntaxes parameters and options>
      CHECK, =, @<structType>, listix_command
      //
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT *, tlsxCmdSyntaxHead.desc AS synDesc FROM tlsxCmdSyntaxParams LEFT JOIN tlsxCmdSyntaxHead USING (cmdName, synIndx) WHERE cmdName = '@<name>' ORDER BY synIndx+0, paramIndx+0;
      @<addOneSyntax>
      =,,

   <addOneSyntax>
      SETVAR, current syntax index, @<synIndx>
      //      --------- SYNTAX @<synIndx> ------------------------------
      //      @<print syntax desc>
      //      @<cmdName>, @<syntax parameters>
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT * FROM tlsxCmdSyntaxOpts WHERE cmdName = '@<name>' AND synIndx = '@<current syntax index>';
      //       , @<optionName>, @<parameters>
      =,,
      @<detail parameters & options>
      //
      //

   <print syntax desc>
      CHECK, =, @<kDetails selected>, 1
      //@<synDesc>
      //

   <syntax parameters>
      RUN LOOP, paramName,
              , WHILE, synIndx
              , LINK , ", "

   <detail parameters & options>
      CHECK, =, @<kDetails selected>, 1
<!      //      -- PARAMETERS DETAIL:
      //
      //
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT *, paramIndx+1 AS parNo FROM tlsxCmdSyntaxParams WHERE cmdName = '@<name>' AND synIndx = '@<current syntax index>' ORDER BY paramIndx+0;
      //        P) @<paramName> [@<defVal>] @<print desc>
      =,,
      //
      //
<!      //      --- OPTIONS DETAIL:
      //
      LOOP, SQL, @<gastonaDoc.db>, //SELECT * FROM tlsxCmdSyntaxOpts WHERE cmdName = '@<name>' AND synIndx = '@<current syntax index>';
      //        O) @<optionName> [@<defVal>] @<print desc>

   <! samples, iSamples and xSample
   <!

   <get samples>
      -->, iSamples, sqlSelect, //SELECT * FROM tGastExamples WHERE name = '@<name>' AND gastSample != 'data4Tester';

   <-- eSearchSample>
      @<-- bSearchInSamples>

   <-- bSearchInSamples>
      -->, iSamples, sqlSelect, @<sql for samples>

   <sql for samples>
      //SELECT * FROM (SELECT * FROM tGastExamples @<Where&OrderBy from FilterSample>) WHERE gastSample != 'data4Tester';

   <-- iSamples>
	   CHECK, VAR, iSamples selected.gastSample

      -->, lSampleTitle,, //@<iSamples selected.gastSample> (@<iSamples selected.name>)
      GEN, @<gastTemp.gast>, sampleAsText
      MSG, xSample load

	<sampleAsText>
	   CHECK, VAR, iSamples selected.sampleBody
	   STRCONV, STRE-TEXT, iSamples selected.sampleBody

   <print desc>
      STRCONV, STRE-TEXT, desc

   <"> CHECK, LINUX, //"

   <-- bRun Sample>
      MSG, xSample save
      @<-- bClearConsole1>
      @<-- bClearConsole2>
      LAUNCH, //@<tool java> -cp @<">@<:sys java.class.path>@<"> gastona.gastona @<">@<gastTemp.gast>@<">

   <-- menuAbout>
      JAVA STATIC, gastona.gastona, showAboutGastona


   <Where&OrderBy from FilterTree>
      JAVA STATIC, javaj.widgets.table.util.utilAsiste, getComposedWhereCondition, @<eFilterTree>, name, groupInfo, desc, helpText

   <Where&OrderBy from FilterSample>
      JAVA STATIC, javaj.widgets.table.util.utilAsiste, getComposedWhereCondition, @<eSearchSample>, name, gastSample, sampleBody

#**#
