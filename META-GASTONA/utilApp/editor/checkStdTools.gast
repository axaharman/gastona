#gastona#

   <fusion>
      META-GASTONA/utilApp/editor/EmergencyEditor.gasti

#javaj#

   <frames>
      Fmain, "Test Standard tool environment", 400, 300

   <layout of Fmain>
      EVA, 10, 10, 7, 7

         ,                , X
         , bTest java     , oConsole
         , bTest Gastona  , +
         , bTest Editor   , +
         , bTest Editor II, +
         , bTest Browser  , +
         , bTest Browser II, +
       X ,                , +

#listix#

   <main>
      SETVAR, t1, @<:lsx tmp html>
      GEN,, gastonaLocal and set standard tools, listix, META-GASTONA/utilApp/std/stdTools.lsx

      DUMP, data

   <-- bTest java>
      //
      // TEST calling java:
      //
      //
      CALL, @<tool java> -version
      //
      //

   <"> CHECK, LINUX, //"

   <-- bTest Gastona>
      GEN, @<t1>, miniGast
      LAUNCH, //@<tool gastona> @<">@<t1>@<">

   <-- bTest Editor>
      GEN, @<t1>, miniGast
      LAUNCH, //@<tool editor> @<">@<t1>@<">

   <-- bTest Editor II>
      GEN, @<t1>, miniGast
      LISTIX, run edit(file line), @<t1>, 9

   <-- bTest Browser>
      GEN, @<t1>, miniHtml
         , PARAMS, I
      LAUNCH, //@<tool browser> @<">@<t1>@<">

   <-- bTest Browser II>
      GEN, @<t1>, miniHtml
         , PARAMS, II
      LISTIX, run browse(file), @<t1>


   <miniGast>
      //#javaj#
      //
      //    <frames> F, Test Gastona
      //
      //    <layout of F>
      //       PANEL, X
      //       lTest Gastona Ok, bClose
      //
      //#listix#
      //
      //    <-- bClose> MSG, javaj doExit
      //

   <miniHtml>
      //<html>
      //    <body>
      //       <h2> Test html browser @<p1> OK</h2>
      //    </body>
      //</html>
      //
      