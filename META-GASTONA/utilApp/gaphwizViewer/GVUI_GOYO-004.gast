#javaj#


   <frames> F, Graphwiz viewer, 500, 400

   <layout of F>
      EVA, 10, 10, 7, 7

         ,        ,  X
       X , left   , right

   <layout of left>

      EVA, 3, 3, 2, 2

         , 200
         , lGraphwiz script
       X , aSources
       5 ,
         , lEngine
         , iEngine

   <layout of right>
      TABBED,

      Image  , mImage
      Source , xFonte
      Console, oConso

   <sysDefaultFonts> Courier, 15, 0, TextField

#data#

   <aSources rootTitle> Graphwiz sources

   <options> ""
   <outputType> gif

   <HOMEAPP>  //@<:sys user.home>@</>gastonaApps@</>graphwizViewer
   <CACHEDIR> //@<HOMEAPP>@</>cache
   <DOTBIN>   //C:\Program Files\Graphviz2.20\bin

   <iEngine selectedIndices>
      indx
      0
      
   <iEngine>
      name

      dot
      neato
      twopi
      circo
      fdp


#listix#

   <void>
   
   </>
      CHECK, LINUX, \
      /

   <main0>
      SCAN, ADD FILES,, ., +, gv, +, dot
      GEN, @<CACHEDIR>@</>.createdir, void
      SETVAR, aSources separator, @</>
      -->, aSources, sqlSelect, //SELECT fullPath FROM scan_all;

   <-- aSources>
      CHECK, VAR, aSources selectedPath
      @<get subPath>
      @<source to text>
      @<show new selection>

   <-- iEngine>
      @<show new selection>

   <get subPath>
      <! We need not just the fullpath but the subPath (without root) as well to form the cached image
      LOOP, SQL,, //SELECT subPath, pathFile, fullPath FROM scan_all WHERE fullPath = '@<aSources selectedPath>';
      @<collect selected node>

   <collect selected node>
      SETVAR, selected_SubPath, @<subPath>
      SETVAR, selected_FullPath, @<fullPath>
      <! ensure output file sub dir
      GEN, @<CACHEDIR>@</>@<pathFile>@</>.createdir, void

   <show new selection>
      CHECK, VAR, selected_SubPath
      CHECK, VAR, iEngine selected.name
      CHECK, FILE    , @<fullPath of output file>
           , ELSE    , @<do generate gif>
           , CONTINUE, 1
      -->, mImage,, @<fullPath of output file>

   <source to text>
      SETVAR, xFonte fileName, @<selected_FullPath>
      MSG, xFonte load

   <do generate gif>
      CHECK, VAR, selected_SubPath
      CHECK, VAR, iEngine selected.name

      //generating @<fullPath of output file> ...
      CALL, //"@<fullPath of selected engine>" @<options> -T@<outputType> -o "@<fullPath of output file>" "@<xFonte fileName>"
      //
      //

   <-- bOpen gif>
      LAUNCH, //CMD /C start "" "@<fullPath of output file>"

   <fullPath of selected engine>  //@<DOTBIN>@</>@<iEngine selected.name>
   <fullPath of output file>      //@<CACHEDIR>@</>@<selected_SubPath>_@<iEngine selected.name>.@<outputType>

#**#
