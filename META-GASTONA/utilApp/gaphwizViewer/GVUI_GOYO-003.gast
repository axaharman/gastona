#javaj#


   <frames> F, Graphwiz viewer, 500, 400

   <layout of F>
      EVA, 10, 10, 7, 7

         ,        ,  X
       X , left   , right

   <layout of left>

      EVA, 3, 3, 2, 2

         , 200
         , lGraphwiz script
       X , aSources
       5 ,
         , lEngine
         , iEngine

   <layout of right>
      TABBED,

      Image  , mImage
      Source , xFonte
      Console, oConso

   <sysDefaultFonts> Courier, 15, 0, TextField


#data#

   <aSources rootTitle> Graphwiz sources

   <options> ""
   <outputType> gif

   <CACHEDIR/> //OUT\
   <DOTBIN/>   //C:\Program Files\Graphviz2.20\bin\

   <iEngine selectedIndices>
      //index
      //0

   <iEngine>
      name

      dot
      neato
      twopi
      circo
      fdp


#listix#

   <void>

   <main0>
      SETVAR, mySrcTemp, @<:lsx tmp gv>
      SCAN, ADD FILES,, ., +, gv, +, dot
      GEN, @<CACHEDIR/>.createdir, void

      SETVAR, aSources separator, \
      -->, aSources, sqlSelect, //SELECT fullPath FROM scan_all;

   <-- aSources>
      CHECK, VAR, aSources selectedPath
      LOOP, SQL,, //SELECT subpath, pathFile, fullPath FROM scan_all WHERE fullPath = '@<aSources selectedPath>';
      @<collect selected node>
      =,,
      @<source to text>
      @<show output>

   <-- iEngine>
      @<show output>

   <collect selected node>
      SETVAR, selected_SubPath, @<subpath>
      SETVAR, selected_FullPath, @<fullPath>
      <! ensure output file sub dir
      GEN, @<CACHEDIR/>@<pathFile>\.createdir, void

   <show output>
      CHECK, VAR, selected_SubPath
      CHECK, VAR, iEngine selected.name
      CHECK, FILE, @<output file>, @<procesa>
           , CONTINUE, 1
      -->, mImage,, @<output file>

   <source to text>
      SETVAR, xFonte fileName, @<selected_FullPath>
      MSG, xFonte load
      SETVAR, xFonte fileName, @<mySrcTemp>
      MSG, xFonte save

   <procesa>
      CHECK, VAR, selected_SubPath
      CHECK, VAR, iEngine selected.name

      //generating @<output file> ...
      CALL, //"@<engine exe>" @<options> -T@<outputType> -o "@<output file>" "@<mySrcTemp>"
      //
      //

   <-- bOpen gif>
      LAUNCH, //CMD /C start "" "@<output file>"

   <engine exe>  //@<DOTBIN/>@<iEngine selected.name>
   <output file> //@<CACHEDIR/>@<selected_SubPath>_@<iEngine selected.name>.@<outputType>

#**#
