#javaj#

   <layout of layFilesAndGroups>
      Eva, 10, 10, 3, 3

       ,   X
     X , sFiles
     A , fileBotons
       , vistaFiltros


   <layout of vistaFiltros>
        EVA, 5, 5, 5, 5
        rrr,  224,          224,      X
           ,   lExtension, lFecha,    lPath
        120,   tExtenFilt, tDateFilt, tPathFilt


#data#

<!   <kGroups>  Ver agrupaciones

   <filterExt>   (1)
   <filterPath>  (1)
   <filterDate>  (1)

   <groups ACTIVE> 0

#listix#

   <main0>
      IN CASE, @<groups ACTIVE>, ==,  1, JAVAJ, MASK, layFiles, layFilesAndGroups

<!   <check groups layout>
<!      CHECK, VAR, kGroups selected
<!      IN CASE, @<kGroups selected>
<!             , 1   , JAVAJ, MASK, layFiles, layFilesAndGroups
<!             , ELSE, JAVAJ, MASK, layFiles

<!   <-- kGroups>
<!      IN CASE, @<kGroups selected>
<!             , 1   , @<activa grupos>
<!             , ELSE, @<desactiva grupos>

   <-- ACTIVATE GROUPS>
      SET VAR, groups ACTIVE, 1
      JAVAJ, MASK, layFiles, layFilesAndGroups
      @<doCalculateGroups>

   <-- DEACTIVATE GROUPS>
      SET VAR, groups ACTIVE, 0
      @<resetFilterGroup>

   <-- NEW ROOT SELECTED>
      @<resetFilterGroup>
      IN CASE, @<groups ACTIVE>, ==, 1, @<doCalculateGroups>

   <middleSelectGroup>
      //, count(*) AS nFiles, ROUND (SUM(size)/1000000, 3) AS MB FROM scan_files WHERE rootID = @<iRoots selected.rootID> GROUP BY

   <doCalculateGroups>
      -->, tExtenFilt, sqlSelect, //SELECT extension @<middleSelectGroup> extension ORDER BY 0+nFiles DESC
      -->, tPathFilt,  sqlSelect, //SELECT pathFile @<middleSelectGroup> pathFile ORDER BY 0+nFiles DESC
      -->, tDateFilt,  sqlSelect, //SELECT substr(date,1,7) as mes @<middleSelectGroup> mes ORDER BY mes DESC

   <-- tExtenFilt>
      @<mount filter extension>
      @<refresh sFiles>

   <-- tPathFilt>
      @<mount filter path>
      @<refresh sFiles>

   <-- tDateFilt>
      @<mount filter date>
      @<refresh sFiles>

   <resetFilterGroup>
      SETVAR, filterExt,   (1)
      SETVAR, filterPath,  (1)
      SETVAR, filterDate,  (1)

   <mount filter extension>
     SETVAR, filterExt, (1)
     CHECK, VAR, tExtenFilt selected.extension
     SETVAR, filterExt, @<form selected filter extension>

   <mount filter path>
     SETVAR, filterPath, (1)
     CHECK, VAR, tPathFilt selected.pathFile
     SETVAR, filterPath, @<form selected filter path>

   <mount filter date>
     SETVAR, filterDate, (1)
     CHECK, VAR, tDateFilt selected.mes
     SETVAR, filterDate, @<form selected filter date>

   <form selected filter extension>
     LOOP, EVA, tExtenFilt subTableSelection
          , LINK, " OR "
      //extension = '@<extension>'

   <form selected filter path>
     LOOP, EVA, tPathFilt subTableSelection
          , LINK, " OR "
      //pathFile = '@<pathFile>'

   <form selected filter date>
     LOOP, EVA, tDateFilt subTableSelection
          , LINK, " OR "
      //date LIKE '@<mes>%'


#**#
