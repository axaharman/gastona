#javaj#

   <layout of layFilesAndGroupSame>
      Eva, 10, 10, 3, 3

       ,   X
     X , sFiles
     A , fileBotons
       , vistaSame


   <layout of vistaSame>
        EVA, 5, 5, 5, 5
           ,             ,        ,        ,        , X
           , lSospechosos, kNombre, kFecha , kTamany,
        120, tSameBrowse , -, -, -, -

#data#

   <lSospechosos> //Lista de sospechosos de estar repetidos por tener igual ...

   <kNombre selected> 1
   <kFecha selected> 1
   <kTamany selected> 1

   <groupSame ACTIVE> 0

#listix#

   <main0>
      IN CASE, @<groupSame ACTIVE>, ==,  1, JAVAJ, MASK, layFiles, layFilesAndGroupSame

   <-- kNombre>   @<doCalculateGroupSame>
   <-- kFecha>    @<doCalculateGroupSame>
   <-- kTamayo>   @<doCalculateGroupSame>

   <-- ACTIVATE GROUP SAME>
      SET VAR, groupSame ACTIVE, 1
      JAVAJ, MASK, layFiles, layFilesAndGroupSame
      @<doCalculateGroupSame>

   <-- DEACTIVATE GROUP SAME>
      SET VAR, groupSame ACTIVE, 0
      @<resetFilterGroup>

   <-- NEW ROOT SELECTED>
      @<resetFilterGroup>
      IN CASE, @<groupSame ACTIVE>, ==, 1, @<doCalculateGroupSame>

   <doCalculateGroupSame>
      -->, tSameBrowse, sqlSelect, @<sql selectGroupSame>

   <sql selectGroupSame>
      //SELECT COUNT(*) AS nFiles, ROUND (SUM(size)/1000000, 3) AS MB @<columns4GroupSame> FROM scan_files WHERE rootID = @<iRoots selected.rootID> GROUP BY "" @<columns4GroupSame> HAVING nFiles > 1 ORDER BY nFiles DESC

<!<!   <sql selectGroupSame>
<!<!      //SELECT
<!<!      //    COUNT(*) AS nFiles,
<!<!      //    ROUND (SUM(size)/1000000, 3) AS MB
<!<!      //    @<columns4GroupSame>
<!<!      //FROM scan_files
<!<!      //WHERE rootID = @<iRoots selected.rootID>
<!<!      //GROUP BY "" @<columns4GroupSame>
<!<!      //HAVING nFiles > 1
<!<!      //ORDER BY nFiles DESC


   <columns4GroupSame>
      IF, @<kNombre selected>, ==, 1, // , fileName
      IF, @<kFecha selected> , ==, 1, // , date
      IF, @<kTamany selected>, ==, 1, // , size

   <-- tSameBrowse>
      SETVAR, filterExt, (1)
      CHECK, VAR, tSameBrowse selected.nFiles
      SETVAR, filterExt, @<form selected filter same>
      @<refresh sFiles>

   <form selected filter same>
     LOOP, EVA, tSameBrowse subTableSelection
          , LINK, " OR "
      //(@<same one record>)

   <same one record>
      (1)
      IF, @<kNombre selected>, ==, 1, // AND fileName = '@<fileName>'
      IF, @<kFecha selected> , ==, 1, // AND date = '@<date>'
      IF, @<kTamany selected>, ==, 1, // AND size+0 = @<size>+0

#**#
