#javaj#

   <layout of layExport>
        EVA, 5, 5, 5, 5
        rrr,                ,                       ,                      ,     X
           ,   lOptions     , kOnly selected records, kInclude column names, rgRadioSeparator
           ,   lColumns     , eColumns   , -, -, bAllColumns
           ,   lExportBase  , eExportName, -, -, bAutoExportName
           ,   bExport      , -


#data#

   <rgRadioSeparator orientation> X
   <rgRadioSeparator titledBorder> "Separator"
   <rgRadioSeparator>
      label       , separator

      Tabulator   ,  @<TAB>
      Colon       ,  ";"
      Comma       ,  ","
      White space ,  " "

   <rgRadioSeparator selectedIndices>
      index
      0

   <TAB> "	"
   <SEPARATOR> @<TAB>

   <kExport>  Dialogo Export

   <eColumns> //pathRoot, pathFile, fileName, extension, date, size, fullPath

   <eExportName>   export\fichero.txt
   <lExportBase>   Exportar a
   <kOnly selected records> Sólo los seleccionados
   <kInclude column names> Incluir nombres columnas

   <bAllColumns> Todas las columnas
   <bAutoExportName> Generar nombre

   <bExport> Exportar

#listix#

   <main0>
      @<check export layout>

   <-- kExport>
      @<check export layout>

   <check export layout>
      CHECK, VAR, kExport selected
      IN CASE, @<kExport selected>
             , 1   , JAVAJ, MASK, fileBotons, layExport
             , ELSE, JAVAJ, MASK, fileBotons,

   <-- bAutoExportName>
      -->, eExportName,, export\@<:lsx date yyyy_MM_dd>\@<:lsx date hh_mm_ss>.xls

   <-- bAllColumns>
      -->, eColumns,, @<allColumns>

   <allColumns>
      LOOP, SQL,, //SELECT * FROM (@<sFiles sqlSelect>) LIMIT 1
          , LINK, ""
      @<link columns>

   <link columns>
      LOOP, COLUMNS
          , LINK, ", "
          ,, @<columnName>


   <-- rgRadioSeparator>
      SETVAR, SEPARATOR, @<rgRadioSeparator selected.separator>

   <ONLY SUB SELECTION> @<kOnly selected records selected>
   <COLUMN NAMES>       @<kInclude column names selected>

   <-- bExport>
      GENERATE, @<eExportName>, ExportLoop

   <ExportLoop>
      IF, @<ONLY SUB SELECTION>
        , 1   , @<export loop subselection>
        , ELSE, @<export loop sql>

   <export loop subselection>
      LOOP, EVA, sFiles subTableSelection
      @<loop all columns>

   <export loop sql>
      LOOP, SQL,, //SELECT @<eColumns> FROM (@<sFiles sqlSelect>) @<sFiles sqlExtraFilter>
      @<loop all columns>

   <loop all columns>
      @<first column names>
      LOOP, COLUMNS
          , LINK, @<SEPARATOR>
      @<columnValue>

   <first column names>
      CHECK, =, @<COLUMN NAMES>, 1
      CHECK, =, @<:lsx ROW>, 0
      LOOP, COLUMNS
          , LINK, @<SEPARATOR>
      @<columnName>
      =,,
      //
      //


 #**#
   